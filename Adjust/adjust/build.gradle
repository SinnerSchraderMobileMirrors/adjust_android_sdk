apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

def getVersionName() {
    return "4.10.5"
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 1
        versionName getVersionName()
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

task clearJar(type: Delete) {
    delete "build/outputs/adjust-${getVersionName()}.jar"
}

task makeJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('build/outputs/')
    include('classes.jar')
    //rename ('classes.jar', "adjust-${getVersionName()}.jar")
}

makeJar.dependsOn(clearJar, build)

task androidJavadocs(type: Javadoc) {
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    source = android.sourceSets.main.java.srcDirs
}

task androidJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
    //from javadoc
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
    //from sourceSets.main.allSource
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Adjust Android SDK'
                packaging 'jar'
                artifactId 'adjust-android'
                groupId 'com.adjust.sdk'
                version "${getVersionName()}"
                description 'The Adjust SDK for Android'
                url 'https://github.com/adjust/android_sdk'

                scm {
                    connection 'scm:git:git@github.com:adjust/android_sdk.git'
                    developerConnection 'scm:git:git@github.com:adjust/android_sdk.git'
                    url 'git@github.com:adjust/android_sdk.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        name 'Pedro Silva'
                        email 'pedro@adjust.com'
                        organization 'adjust GmbH'
                        organizationUrl 'http://www.adjust.com'
                    }
                }
            }
        }
    }
}
